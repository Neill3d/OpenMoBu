; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "OpenMoBu for Autodesk® MotionBuilder®"
#define MyAppVersion "2021.0"
#define MyAppPublisher "Neill3d"
#define MyAppURL "https://github.com/Neill3d/OpenMoBu"
#define MyAppExeName "Setup_OpenMoBu_2021_0"

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"; InfoBeforeFile:"infoBefore_en.txt"; InfoAfterFile: "infoAfter_en.txt"
Name: "russian"; MessagesFile: "compiler:Languages\Russian.isl"; InfoBeforeFile:"infoBefore_ru.txt"; InfoAfterFile: "infoAfter_ru.txt"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{2A0E1623-47E7-49DD-87AA-0BAB1C1226B0}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
CreateAppDir=yes
OutputBaseFilename={#MyAppExeName}
Compression=lzma
SolidCompression=yes
AppCopyright=Copyright (C) 2019-2021 Neill3d
WizardImageFile=Neill3d2.bmp
WizardImageStretch=no
WizardSmallImageFile=Neill3d_sm.bmp
DisableWelcomePage=no
DisableDirPage = auto
DefaultDirName = {autopf}\OpenMoBu
AlwaysShowDirOnReadyPage = yes
ChangesEnvironment=true

[Types]
;Name: "full"; Description: "Full installation"
Name: "2014"; Description: "MotionBuilder 2014"
Name: "2015"; Description: "MotionBuilder 2015"
Name: "2016"; Description: "MotionBuilder 2016"
Name: "2017"; Description: "MotionBuilder 2017"
Name: "2018"; Description: "MotionBuilder 2018"
Name: "2019"; Description: "MotionBuilder 2019"

;Name: "custom"; Description: "Custom installation"; Flags: iscustom

[Components]
Name: "MotionBuilder2014"; Description: "MotionBuilder 2014 plugins"; Types: 2014
; Types: full 2014 custom;
Name: "MotionBuilder2015"; Description: "MotionBuilder 2015 plugins"; Types: 2015
; Types: full 2015 custom;
Name: "MotionBuilder2016"; Description: "MotionBuilder 2016 plugins"; Types: 2016
; full 2016 custom;
Name: "MotionBuilder2017"; Description: "MotionBuilder 2017 plugins"; Types: 2017
; full 2017 custom;
Name: "MotionBuilder2018"; Description: "MotionBuilder 2018 plugins"; Types: 2018
; full 2018 custom;
Name: "MotionBuilder2019"; Description: "MotionBuilder 2019 plugins"; Types: 2019
; full 2019 custom;

[Tasks]
Name: envPath; Description: "Add to PATH variable" 
Name: relationConstraintBox; GroupDescription: "Plugins:"; Description: "Relation Constraint Boxes";
Name: relationConstraintBox\poseReader; GroupDescription: "Plugins:"; Description: "Pose Reader";
Name: relationConstraintBox\boxRBF; GroupDescription: "Plugins:"; Description: "RBF Box";
Name: relationConstraintBox\rayIntersect; GroupDescription: "Plugins:"; Description: "Ray Intersect";
Name: relationConstraintBox\boxSpring; GroupDescription: "Plugins:"; Description: "Spring Box";
Name: constraint; GroupDescription: "Plugins:"; Description: "Constrains";
Name: constraint\attachmentConstraint; GroupDescription: "Plugins:"; Description: "Attachment Constraint";
Name: constraint\drivenConstraint; GroupDescription: "Plugins:"; Description: "Driven Constraint";
Name: device; GroupDescription: "Plugins:"; Description: "Devices";
Name: device\faceCap; GroupDescription: "Plugins:"; Description: "FaceCap Device";
Name: device\projectTango; GroupDescription: "Plugins:"; Description: "Project Tango Device";
Name: shaders; GroupDescription: "Plugins:"; Description: "Shaders";
Name: shaders\superDynamicLighting; GroupDescription: "Plugins:"; Description: "Super Dynamic Lighting";
Name: lockCamera; GroupDescription: "Plugins:"; Description: "Lock Camera Manipulator";
Name: postProcessing; GroupDescription: "Plugins:"; Description: "Post Processing FX";
Name: references; GroupDescription: "Plugins:"; Description: "References";
Name: references\manager; GroupDescription: "Plugins:"; Description: "References Manager";
Name: references\extension; GroupDescription: "Plugins:"; Description: "FBX References Extension";
Name: calculateNormals; GroupDescription: "Plugins:"; Description: "Calculate Normals Solver";
Name: volumeCalculator; GroupDescription: "Plugins:"; Description: "Optical MoCap Volume Calculator";

Name: scripts; GroupDescription: "Scripts:"; Description: "Scripts";
Name: scripts\startup; GroupDescription: "Scripts:"; Description: "StartUp Scripts";
Name: scripts\references; GroupDescription: "Scripts:"; Description: "StartUp References";
Name: scripts\actions; GroupDescription: "Scripts:"; Description: "Action Scripts";

[Files]

; 2014

Source: "..\bin\x64\plugins_2014\*.dll"; DestDir: "{code:MoBu_Path64|2014}\bin\x64\plugins\"; Flags: ignoreversion; Components: MotionBuilder2014
;Source: "..\bin\x64\plugins_2014\GLSL\*"; DestDir: "{code:MoBu_Path64|2014}\bin\x64\plugins\GLSL\"; Flags: ignoreversion; Components: MotionBuilder2014
;Source: "..\bin\x64\plugins_2014\GLSL_CS\*"; DestDir: "{code:MoBu_Path64|2014}\bin\x64\plugins\GLSL_CS\"; Flags: ignoreversion; Components: MotionBuilder2014
Source: "..\PythonScripts\Startup\*"; DestDir: "{userdocs}\MB\2014-x64\config\PythonStartup\"; Flags: ignoreversion; Components: MotionBuilder2014
Source: "..\PythonScripts\Startup_Qt4\*"; DestDir: "{userdocs}\MB\2014-x64\config\PythonStartup\"; Flags: ignoreversion; Components: MotionBuilder2014
Source: "..\PythonScripts\Startup_Qt4\MBFileRefAdvanced\*"; DestDir: "{userdocs}\MB\2014-x64\config\PythonStartup\MBFileRefAdvanced\"; Flags: ignoreversion; Components: MotionBuilder2014
Source: "..\PythonScripts\Actions\*"; DestDir: "{code:MoBu_Path64|2014}\bin\config\Scripts\Neill3d\"; Flags: ignoreversion; Components: MotionBuilder2014

; 2015

Source: "..\bin\x64\plugins_2015\*.dll"; DestDir: "{code:MoBu_Path64|2015}\bin\x64\plugins\"; Flags: ignoreversion; Components: MotionBuilder2015
;Source: "..\bin\x64\plugins_2015\GLSL\*"; DestDir: "{code:MoBu_Path64|2015}\bin\x64\plugins\GLSL\"; Flags: ignoreversion; Components: MotionBuilder2015
;Source: "..\bin\x64\plugins_2015\GLSL_CS\*"; DestDir: "{code:MoBu_Path64|2015}\bin\x64\plugins\GLSL_CS\"; Flags: ignoreversion; Components: MotionBuilder2015
Source: "..\PythonScripts\Startup\*"; DestDir: "{userdocs}\MB\2015-x64\config\PythonStartup\"; Flags: ignoreversion; Components: MotionBuilder2015
Source: "..\PythonScripts\Startup_Qt4\*"; DestDir: "{userdocs}\MB\2015-x64\config\PythonStartup\"; Flags: ignoreversion; Components: MotionBuilder2015
Source: "..\PythonScripts\Startup_Qt4\MBFileRefAdvanced\*"; DestDir: "{userdocs}\MB\2015-x64\config\PythonStartup\MBFileRefAdvanced\"; Flags: ignoreversion; Components: MotionBuilder2015
Source: "..\PythonScripts\Actions\*"; DestDir: "{code:MoBu_Path64|2015}\bin\config\Scripts\Neill3d\"; Flags: ignoreversion; Components: MotionBuilder2015

; 2016

Source: "..\bin\x64\plugins_2016\*.dll"; DestDir: "{code:MoBu_Path64|2016}\bin\x64\plugins\"; Flags: ignoreversion; Components: MotionBuilder2016
;Source: "..\bin\x64\plugins_2016\GLSL\*"; DestDir: "{code:MoBu_Path64|2016}\bin\x64\plugins\GLSL\"; Flags: ignoreversion; Components: MotionBuilder2016
;Source: "..\bin\x64\plugins_2016\GLSL_CS\*"; DestDir: "{code:MoBu_Path64|2016}\bin\x64\plugins\GLSL_CS\"; Flags: ignoreversion; Components: MotionBuilder2016
Source: "..\PythonScripts\Startup\*"; DestDir: "{userdocs}\MB\2016-x64\config\PythonStartup\"; Flags: ignoreversion; Components: MotionBuilder2016
Source: "..\PythonScripts\Startup_Qt4\*"; DestDir: "{userdocs}\MB\2016-x64\config\PythonStartup\"; Flags: ignoreversion; Components: MotionBuilder2016
Source: "..\PythonScripts\Startup_Qt4\MBFileRefAdvanced\*"; DestDir: "{userdocs}\MB\2016-x64\config\PythonStartup\MBFileRefAdvanced\"; Flags: ignoreversion; Components: MotionBuilder2016
Source: "..\PythonScripts\Actions\*"; DestDir: "{code:MoBu_Path64|2016}\bin\config\Scripts\Neill3d\"; Flags: ignoreversion; Components: MotionBuilder2016

; 2017

Source: "..\bin\x64\plugins_2017\*.dll"; DestDir: "{code:MoBu_Path64|2017}\bin\x64\plugins\"; Flags: ignoreversion; Components: MotionBuilder2017
Source: "..\bin\x64\plugins_2017\FBX\*.dll"; DestDir: "{code:MoBu_Path64|2017}\bin\x64\plugins\FBX\"; Flags: ignoreversion; Components: MotionBuilder2017
Source: "..\bin\x64\plugins_2017\GLSL\*"; DestDir: "{code:MoBu_Path64|2017}\bin\x64\plugins\GLSL\"; Flags: ignoreversion; Components: MotionBuilder2017
Source: "..\bin\x64\plugins_2017\GLSL_CS\*"; DestDir: "{code:MoBu_Path64|2017}\bin\x64\plugins\GLSL_CS\"; Flags: ignoreversion; Components: MotionBuilder2017
Source: "..\PythonScripts\Startup\*"; DestDir: "{userdocs}\MB\2017-x64\config\PythonStartup\"; Flags: ignoreversion; Components: MotionBuilder2017
Source: "..\PythonScripts\Startup_Qt4\*"; DestDir: "{userdocs}\MB\2017-x64\config\PythonStartup\"; Flags: ignoreversion; Components: MotionBuilder2017
Source: "..\PythonScripts\Startup_Qt4\MBFileRefAdvanced\*"; DestDir: "{userdocs}\MB\2017-x64\config\PythonStartup\MBFileRefAdvanced\"; Flags: ignoreversion; Components: MotionBuilder2017
Source: "..\PythonScripts\Actions\*"; DestDir: "{code:MoBu_Path64|2017}\bin\config\Scripts\Neill3d\"; Flags: ignoreversion; Components: MotionBuilder2017

; 2018

Source: "..\bin\x64\plugins_2018\*.dll"; DestDir: "{code:MoBu_Path64|2018}\bin\x64\plugins\"; Flags: ignoreversion; Components: MotionBuilder2018
Source: "..\bin\x64\plugins_2018\FBX\*.dll"; DestDir: "{code:MoBu_Path64|2018}\bin\x64\plugins\FBX\"; Flags: ignoreversion; Components: MotionBuilder2018
Source: "..\bin\x64\plugins_2018\GLSL\*"; DestDir: "{code:MoBu_Path64|2018}\bin\x64\plugins\GLSL\"; Flags: ignoreversion; Components: MotionBuilder2018
Source: "..\bin\x64\plugins_2018\GLSL_CS\*"; DestDir: "{code:MoBu_Path64|2018}\bin\x64\plugins\GLSL_CS\"; Flags: ignoreversion; Components: MotionBuilder2018
Source: "..\PythonScripts\Startup\*"; DestDir: "{userdocs}\MB\2018-x64\config\PythonStartup\"; Flags: ignoreversion; Components: MotionBuilder2018
Source: "..\PythonScripts\Startup_Qt4\*"; DestDir: "{userdocs}\MB\2018-x64\config\PythonStartup\"; Flags: ignoreversion; Components: MotionBuilder2018
Source: "..\PythonScripts\Startup_Qt4\MBFileRefAdvanced\*"; DestDir: "{userdocs}\MB\2018-x64\config\PythonStartup\MBFileRefAdvanced\"; Flags: ignoreversion; Components: MotionBuilder2018
Source: "..\PythonScripts\Actions\*"; DestDir: "{code:MoBu_Path64|2018}\bin\config\Scripts\Neill3d\"; Flags: ignoreversion; Components: MotionBuilder2018

; 2019

Source: "..\bin\x64\plugins_2019\box_poseReader.dll"; DestDir: "{app}\bin\x64\plugins\"; Flags: ignoreversion; Components: MotionBuilder2019; Tasks: relationConstraintBox\poseReader
Source: "..\bin\x64\plugins_2019\box_rayIntersect.dll"; DestDir: "{app}\bin\x64\plugins\"; Flags: ignoreversion; Components: MotionBuilder2019; Tasks: relationConstraintBox\rayIntersect
Source: "..\bin\x64\plugins_2019\box_RBF.dll"; DestDir: "{app}\bin\x64\plugins\"; Flags: ignoreversion; Components: MotionBuilder2019; Tasks: relationConstraintBox\boxRBF
Source: "..\bin\x64\plugins_2019\box_spring.dll"; DestDir: "{app}\bin\x64\plugins\"; Flags: ignoreversion; Components: MotionBuilder2019; Tasks: relationConstraintBox\boxSpring

Source: "..\bin\x64\plugins_2019\constraint_attachment.dll"; DestDir: "{app}\bin\x64\plugins\"; Flags: ignoreversion; Components: MotionBuilder2019; Tasks: constraint\attachmentConstraint
Source: "..\bin\x64\plugins_2019\driven_constraint.dll"; DestDir: "{app}\bin\x64\plugins\"; Flags: ignoreversion; Components: MotionBuilder2019; Tasks: constraint\drivenConstraint

;Source: "..\bin\x64\plugins_2019\device_faceCap.dll"; DestDir: "{app}\bin\x64\plugins\"; Flags: ignoreversion; Components: MotionBuilder2019; Tasks: device\faceCap
Source: "..\bin\x64\plugins_2019\device_projectTango.dll"; DestDir: "{app}\bin\x64\plugins\"; Flags: ignoreversion; Components: MotionBuilder2019; Tasks: device\projectTango

Source: "..\bin\x64\plugins_2019\manip_lockcamera.dll"; DestDir: "{app}\bin\x64\plugins\"; Flags: ignoreversion; Components: MotionBuilder2019; Tasks: lockCamera
Source: "..\bin\x64\plugins_2019\post_processing.dll"; DestDir: "{app}\bin\x64\plugins\"; Flags: ignoreversion; Components: MotionBuilder2019; Tasks: postProcessing
Source: "..\bin\x64\plugins_2019\SuperDynamicLighting.dll"; DestDir: "{app}\bin\x64\plugins\"; Flags: ignoreversion; Components: MotionBuilder2019; Tasks: shaders\superDynamicLighting

Source: "..\bin\x64\plugins_2019\solver_calculateNormals.dll"; DestDir: "{app}\bin\x64\plugins\"; Flags: ignoreversion; Components: MotionBuilder2019; Tasks: calculateNormals
Source: "..\bin\x64\plugins_2019\volume_calculator.dll"; DestDir: "{app}\bin\x64\plugins\"; Flags: ignoreversion; Components: MotionBuilder2019; Tasks: volumeCalculator

Source: "..\bin\x64\plugins_2019\references_manager.dll"; DestDir: "{app}\bin\x64\plugins\"; Flags: ignoreversion; Components: MotionBuilder2019; Tasks: references\manager
Source: "..\bin\x64\plugins_2019\FBX\FBXExtension_References.dll"; DestDir: "{app}\bin\x64\plugins\FBX\"; Flags: ignoreversion; Components: MotionBuilder2019; Tasks: references\extension


Source: "..\bin\x64\plugins_2019\GLSL\*"; DestDir: "{app}\bin\x64\plugins\GLSL\"; Flags: ignoreversion; Components: MotionBuilder2019
Source: "..\bin\x64\plugins_2019\GLSL_CS\*"; DestDir: "{app}\bin\x64\plugins\GLSL_CS\"; Flags: ignoreversion; Components: MotionBuilder2019
Source: "..\PythonScripts\Startup\*"; DestDir: "{app}\bin\config\PythonStartup\"; Flags: ignoreversion; Components: MotionBuilder2019; Tasks: scripts\startup
Source: "..\PythonScripts\Startup_Qt5\*"; DestDir: "{app}\bin\config\PythonStartup\"; Flags: ignoreversion; Components: MotionBuilder2019; Tasks: scripts\startup
Source: "..\PythonScripts\Startup_Qt5\MBFileRefAdvanced\*"; DestDir: "{app}\bin\config\PythonStartup\MBFileRefAdvanced\"; Flags: ignoreversion; Components: MotionBuilder2019; Tasks: scripts\references
Source: "..\PythonScripts\Actions\*"; DestDir: "{app}\bin\config\Scripts\Neill3d\"; Flags: ignoreversion; Components: MotionBuilder2019; Tasks: scripts\actions

;[Registry]
;Root: HKLM; Subkey: "SYSTEM\CurrentControlSet\Control\Session Manager\Environment"; \
;    ValueType: expandsz; ValueName: "Path"; ValueData: "{olddata};C:\foo"; \
;    Check: NeedsAddPath('C:\foo')


; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Code]


const EnvironmentKey = 'SYSTEM\CurrentControlSet\Control\Session Manager\Environment';
const MoBuEnvKey = 'MOTIONBUILDER_PLUGIN_PATH';
const MoBuExtEnvKey = 'MOBU_FBX_EXTENSION_PATH';
const MoBuPythonEnvKey = 'MOTIONBUILDER_PYTHON_STARTUP';

procedure EnvAddPath(Key:string; Path: string);
var
    Paths: string;
begin
    { Retrieve current path (use empty string if entry not exists) }
    if not RegQueryStringValue(HKEY_LOCAL_MACHINE, EnvironmentKey, Key, Paths)
    then Paths := '';

    { Skip if string already found in path }
    if Pos(';' + Uppercase(Path) + ';', ';' + Uppercase(Paths) + ';') > 0 then exit;

    { App string to the end of the path variable }
    Paths := Paths + ';'+ Path +';';

    { Overwrite (or create if missing) path environment variable }
    if RegWriteStringValue(HKEY_LOCAL_MACHINE, EnvironmentKey, Key, Paths)
    then Log(Format('The [%s] added to PATH: [%s]', [Path, Paths]))
    else Log(Format('Error while adding the [%s] to PATH: [%s]', [Path, Paths]));
end;

procedure EnvRemovePath(Key:string; Path: string);
var
    Paths: string;
    P: Integer;
begin
    { Skip if registry entry not exists }
    if not RegQueryStringValue(HKEY_LOCAL_MACHINE, Key, 'Path', Paths) then
        exit;

    { Skip if string not found in path }
    P := Pos(';' + Uppercase(Path) + ';', ';' + Uppercase(Paths) + ';');
    if P = 0 then exit;

    { Update path variable }
    Delete(Paths, P - 1, Length(Path) + 1);

    { Overwrite path environment variable }
    if RegWriteStringValue(HKEY_LOCAL_MACHINE, EnvironmentKey, Key, Paths)
    then Log(Format('The [%s] removed from PATH: [%s]', [Path, Paths]))
    else Log(Format('Error while removing the [%s] from PATH: [%s]', [Path, Paths]));
end;

procedure CurStepChanged(CurStep: TSetupStep);
begin
    if (CurStep = ssPostInstall) and WizardIsTaskSelected('envPath')
    then begin
      EnvAddPath(MoBuEnvKey, ExpandConstant('{app}') + '\bin\x64\plugins');
    end;
end;

procedure CurUninstallStepChanged(CurUninstallStep: TUninstallStep);
begin
    if CurUninstallStep = usPostUninstall
    then EnvRemovePath(MoBuEnvKey, ExpandConstant('{app}') +'\bin\x64\plugins');
end;

function NeedsAddPath(Param: string): boolean;
var
  OrigPath: string;
begin
  if not RegQueryStringValue(HKEY_LOCAL_MACHINE,
    'SYSTEM\CurrentControlSet\Control\Session Manager\Environment',
    'Path', OrigPath)
  then begin
    Result := True;
    exit;
  end;
  { look for the path with leading and trailing semicolon }
  { Pos() returns 0 if not found }
  Result := Pos(';' + Param + ';', ';' + OrigPath + ';') = 0;
end;

function MoBu_Path32(Param: String): String;
var
  key, path: String;
begin
  key := 'Software\Autodesk\MotionBuilder\' + Param;
  Result := '';
  if RegKeyExists(HKEY_LOCAL_MACHINE, key) then
  begin
    If RegQueryStringValue(HKEY_LOCAL_MACHINE, key, 'InstallPath', path) then
    begin
      Result := path;
    end;
  end;
end;

function MoBu_Path64(Param: String): String;
var
  key, path: String;
begin
  key := 'Software\Autodesk\MotionBuilder\' + Param;
  Result := '';
  if IsWin64 and RegKeyExists(HKLM64, key) then
  begin
    If RegQueryStringValue(HKLM64, key, 'InstallPath', path) then
    begin
      Result := path;
    end;
  end;
end;

function IsMoBu(Param: String): Boolean;
begin
  Result := False;
  if RegKeyExists(HKLM, 'Software\Autodesk\MotionBuilder\' + Param) then
  begin
    Result := True;
  end;
end;

function IsMoBu64(Param: String) : Boolean;
begin
  Result := False;
  if IsWin64 and RegKeyExists(HKLM64, 'Software\Autodesk\MotionBuilder\' + Param) then
  begin
    Result := True;
  end;
end;